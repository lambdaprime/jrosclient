plugins {
  id 'com.diffplug.spotless'
  id 'jacoco'
}

apply plugin : 'eclipse'

dependencies {
  implementation project(':jrosclient')
  addDependencies(this, ["implementation", "apiElements"], "1.0-SNAPSHOT", [
    "io.github.lambdaprime:pubsubtests"
  ]);
  addDependencies(this, ["implementation", "apiElements"], "5.4.2", [
    "org.junit.jupiter:junit-jupiter-api",
    "org.junit.jupiter:junit-jupiter-params"
  ]);

  apiElements project(':jrosclient')

  runtimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
  runtimeOnly 'org.junit.platform:junit-platform-commons:1.7.0'
}

sourceCompatibility = '17'
targetCompatibility = '17'

test {
  useJUnitPlatform()

  testLogging {
    events "started", "failed"
  }
}

// run spotless and format code before the build
testClasses.dependsOn spotlessApply

if (project.hasProperty('publish')) {

  apply plugin: 'maven-publish'

  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = 'jrosclient.tests'
        groupId = 'io.github.lambdaprime'
        from components.java
        pom {
          name = 'jrosclient.tests'
          description = 'jrosclient tests'
          url = 'http://portal2.atwebpages.com/jrosclient'

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id = 'lambdaprime'
              email = 'intid@protonmail.com'
            }
          }

          scm {
            url = 'https://github.com/lambdaprime/jrosclient'
          }
        }
      }
    }
  }
}
