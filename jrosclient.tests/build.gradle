plugins {
  id 'com.diffplug.spotless'
  id 'jacoco'
  // only if you build Java library
  id 'java-library'
}

eclipse.classpath.file {
  // instruct eclipse plugin to put everything to module path
  whenMerged {
    entries.findAll { it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
  }
}

dependencies {
  implementation project(':jrosclient')
  api "io.github.lambdaprime:pubsubtests:1.0-SNAPSHOT"

  def junitVersion = "5.8.1"
  implementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  implementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
  runtimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
  runtimeOnly "org.junit.platform:junit-platform-commons:1.7.0"
}

jacoco {
  // Java 21 support
  toolVersion = "0.8.10"
}

test {
  useJUnitPlatform()

  testLogging {
    events "started", "failed"
  }
}

// run spotless and format code before the build
testClasses.dependsOn spotlessApply

if (project.hasProperty('publish')) {

  apply plugin: 'maven-publish'

  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = 'jrosclient.tests'
        groupId = 'io.github.lambdaprime'
        from components.java
        pom {
          name = 'jrosclient.tests'
          description = 'jrosclient tests'
          url = 'http://portal2.atwebpages.com/jrosclient'

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id = 'lambdaprime'
              email = 'intid@protonmail.com'
            }
          }

          scm {
            url = 'https://github.com/lambdaprime/jrosclient'
          }
        }
      }
    }
  }
}
